{-# OPTIONS --guarded #-}

import Prelude as P hiding (_<_ ; Tactic-bishop-finite ; ordâ†’is-discrete)
open P
open import Data.Empty
open import Data.Bool
open import Data.Dec as Dec
open import Data.Sum
open import Data.Nat
open import Data.List
open import Data.List.Correspondences.Unary.Any
open import Correspondences.Wellfounded

open import Data.Star
open import Data.Plus
open import Later
open import Clocked.Partial
open import Clocked.Partial.Converges
open import Clocked.Partial.All

module Examples.DFS
  {â„“ â„“â€² : Level}
  {A : ğ’° â„“}
  â¦ƒ dA : is-discrete A â¦„
  (sucs : A â†’ List A)
  (acy : Wf Î» x y â†’ Has x (sucs y))
  where

private variable
  â„“â€³ : Level
  Îº : Cl

succ : A â†’ A â†’ ğ’° â„“
succ x y = Has x (sucs y)

-- TODO smth / copypasta

has : A â†’ List A â†’ Bool
has = elem (Î» x y â†’ âŒŠ x â‰Ÿ y âŒ‹)

has-r : âˆ€ x xs â†’ Reflects (Has x xs) (has x xs)
has-r x []       = ofâ¿ Â¬Any-[]
has-r x (y âˆ· xs) with x â‰Ÿ y
... | yes e = ofÊ¸ (here (sym e))
... | no ne =
  Reflectsâ€².dmap there
    (Î» h â†’ Î» where
               (here e) â†’ ne (sym e)
               (there a) â†’ h a)
    (has-r x xs)

Subset : List A â†’ List A â†’ ğ’° â„“
Subset xs ys = âˆ€ z â†’ Has z xs â†’ Has z ys

Subset-refl : âˆ€ xs â†’ Subset xs xs
Subset-refl xs z = id

succ-closed : List A â†’ ğ’° â„“
succ-closed l = âˆ€ x y â†’ Has x l â†’ succ y x â†’ Has y l

star-sc : âˆ€ l â†’ succ-closed l
        â†’ âˆ€ x y
        â†’ Has x l
        â†’ Star succ y x
        â†’ Has y l
star-sc l sc x .x hx  Îµ             = hx
star-sc l sc x  y hx (_â—…_ {b} h st) = sc b y (star-sc l sc x b hx st) h

-- well-founded DFS

mutual
  dfs : (x : A) â†’ (a : List A) â†’ (ac : Acc succ x) â†’ List A
  dfs x a ac = if has x a then a else x âˆ· dfs-list (sucs x) a ac (Subset-refl (sucs x))

  -- an inlined left fold
  dfs-list : (l a : List A) â†’ {x : A} â†’ (ac : Acc succ x) â†’ Subset l (sucs x) â†’ List A
  dfs-list  []     a     ac           sub = a
  dfs-list (y âˆ· l) a {x} ac@(acc rec) sub =
    dfs-list l
      (dfs y a (rec y (sub y (here refl))))
      {x} ac
      Î» z Hz â†’ sub z (there Hz)

dfs0 : A â†’ List A
dfs0 x = dfs x [] (acy x)

dfs-correct : (x : A) â†’ (l : List A) â†’ (ac : Acc succ x)
            â†’ succ-closed l
            â†’ âˆ€ y
            â†’ (Has y (dfs x l ac) â†’ Has y l âŠ Star succ y x) Ã— (Has y l âŠ Star succ y x â†’ Has y (dfs x l ac))
dfs-correct = to-induction succ acy (Î» z â†’
                                    (l : List A) (ac : Acc succ z) â†’
                                    succ-closed l â†’
                                    (y : A) â†’
                                    (Has y (dfs z l ac) â†’ Has y l âŠ Star succ y z) Ã—
                                    (Has y l âŠ Star succ y z â†’ Has y (dfs z l ac)))
                           go
  where
  go-list : (x : A)
          â†’ ((y : A) â†’ succ y x â†’ (l : List A) (ac : Acc succ y) â†’ succ-closed l
             â†’ (z : A) â†’ (Has z (dfs y l ac) â†’ Has z l âŠ Star succ z y) Ã— (Has z l âŠ Star succ z y â†’ Has z (dfs y l ac)))
          â†’ (l : List A) â†’ (accu : List A)
          â†’ (ac : Acc succ x)
          â†’ succ-closed accu
          â†’ (sub : Subset l (sucs x))
          â†’ (âˆ€ z â†’ succ z x â†’ Â¬ Has z l â†’ Has z accu)
          â†’ âˆ€ y
          â†’ (Has y (dfs-list l accu ac sub) â†’ Has y accu âŠ Plus succ y x)
          Ã— (Has y accu âŠ Plus succ y x â†’ Has y (dfs-list l accu ac sub))
  go-list x ih []      accu ac           sc sub accm y =
      inl
    , [ id
      , (Î» p â†’ let (s , syc , scx) = Plus-unsnoc p in
               star-sc accu sc s y (accm s scx Â¬Any-[]) syc)
      ]áµ¤
  go-list x ih (w âˆ· l) accu ac@(acc rec) sc sub accm y =
    let swx : succ w x
        swx = sub w (here refl)
        ihw = ih w swx accu (rec w swx) sc
        ihl = go-list x ih l (dfs w accu (rec w swx)) ac
                 (Î» a b ha hs â†’ ihw b .snd ([ (Î» hha â†’ inl (sc a b hha hs)) , (Î» str â†’ inr (hs â—… str)) ]áµ¤ (ihw a .fst ha)))
                 (Î» z Hz â†’ sub z (there Hz))
                 (Î» z Hz Nz â†’ ihw z .snd (Dec.rec (Î» e â†’ inr (subst (Star succ z) e Îµ))
                                                  (Î» Â¬e â†’ inl (accm z Hz (Â¬Any-âˆ· (Â¬e âˆ˜ sym) Nz)))
                                                  (z â‰Ÿ w)))
                 y
      in
      (Î» hh â†’ [ (Î» hp â†’ [ inl , (Î» str â†’ inr (Star-â—… str swx)) ]áµ¤ (ihw y .fst hp) ) , inr ]áµ¤ (ihl .fst hh))
    , (Î» hh â†’ ihl .snd ([ (Î» hy â†’ inl (ihw y .snd (inl hy))) , inr ]áµ¤ hh))

  go : (x : A)
     â†’ ((y : A) â†’ succ y x â†’ (l : List A) (ac : Acc succ y) â†’ succ-closed l
        â†’ (z : A) â†’ (Has z (dfs y l ac) â†’ Has z l âŠ Star succ z y) Ã— (Has z l âŠ Star succ z y â†’ Has z (dfs y l ac)))
     â†’ (l : List A) (ac : Acc succ x) â†’ succ-closed l
     â†’ (y : A) â†’ (Has y (dfs x l ac) â†’ Has y l âŠ Star succ y x)
               Ã— (Has y l âŠ Star succ y x â†’ Has y (dfs x l ac))
  go x ih l ac sc y with has x l | recall (Î» q â†’ has q l) x
  ... | true  | âŸª e âŸ« = inl , [ id , star-sc l sc x y (true-reflects (has-r x l) (subst âŸ¦_âŸ§áµ‡ (sym e) tt)) ]áµ¤
  ... | false | _     =
    let (f , g) = go-list x ih (sucs x) l ac sc
                    (Subset-refl (sucs x)) (Î» z Hz Nz â†’ absurd (Nz Hz)) y
      in
    (Î» where
        (here e)   â†’ inr (subst (Star succ y) (sym e) Îµ)
        (there hy) â†’ [ inl , inr âˆ˜ Plusâ†’Star ]áµ¤ (f hy))
    , [ there âˆ˜ g âˆ˜ inl , [ there âˆ˜ g âˆ˜ inr , (here âˆ˜ sym) ]áµ¤ âˆ˜ Starâ†’Plusï¼ ]áµ¤

-- DFS finds all reachable nodes
dfs0-correct : (x : A)
             â†’ âˆ€ y
             â†’ (Has y (dfs0 x) â†’ Star succ y x) Ã— (Star succ y x â†’ Has y (dfs0 x))
dfs0-correct x y =
  let (f , g) = dfs-correct x [] (acy x) (Î» _ _ h _ â†’ absurd (Â¬Any-[] h)) y in
    [ (Î» h â†’ absurd (Â¬Any-[] h)) , id ]áµ¤ âˆ˜ f
  , g âˆ˜ inr

-- coinductive

-- TODO move + generalize level
foldláµ : {B : ğ’° â„“}
       â†’ (B â†’ A â†’ â–¹ Îº (gPart Îº B))
       â†’ B â†’ List A â†’ gPart Îº B
foldláµ f b []       = now b
foldláµ f b (a âˆ· as) = later (((Î» q â†’ foldláµ f q as) =<<áµ_) â‰ f b a)

-- TODO we should probably have some combinators for foldláµ

dfs-listáµ-body : â–¹ Îº (A â†’ List A â†’ gPart Îº (List A))
               â†’ List A â†’ List A â†’ gPart Îº (List A)
dfs-listáµ-body dâ–¹ l a = foldláµ (Î» y b â†’ dâ–¹ âŠ› next b âŠ› next y) a l

dfsáµ-body : â–¹ Îº (A â†’ List A â†’ gPart Îº (List A))
          â†’ A â†’ List A â†’ gPart Îº (List A)
dfsáµ-body dâ–¹ x a = if has x a then now a else mapáµ (x âˆ·_) (dfs-listáµ-body dâ–¹ (sucs x) a)

dfsáµ : A â†’ List A â†’ gPart Îº (List A)
dfsáµ = fix dfsáµ-body

dfsá¶œ : A â†’ List A â†’ Part (List A)
dfsá¶œ x a Îº = dfsáµ x a

dfs0á¶œ : A â†’ Part (List A)
dfs0á¶œ x = dfsá¶œ x []

-- termination

dfs-listáµ : List A â†’ List A â†’ gPart Îº (List A)
dfs-listáµ = dfs-listáµ-body (dfix dfsáµ-body)

dfs-listá¶œ : List A â†’ List A â†’ Part (List A)
dfs-listá¶œ l a Îº = dfs-listáµ l a

mutual
  dfsá¶œâ‡“ : (x : A) â†’ (a : List A) â†’ Acc succ x â†’ dfsá¶œ x a â‡“
  dfsá¶œâ‡“ x a ac with has x a
  ... | true  = a , âˆ£ 0 , refl âˆ£â‚
  ... | false =
    let (q , qâ‡“) = dfs-listá¶œâ‡“ (sucs x) a ac (Subset-refl (sucs x)) in
    (x âˆ· q) , (mapâ‡“ (x âˆ·_) qâ‡“)

  dfs-listá¶œâ‡“ : (l a : List A) â†’ {x : A} â†’ Acc succ x â†’ Subset l (sucs x) â†’ dfs-listá¶œ l a â‡“
  dfs-listá¶œâ‡“ []      a     ac           sub = a , âˆ£ 0 , refl âˆ£â‚
  dfs-listá¶œâ‡“ (y âˆ· l) a {x} ac@(acc rec) sub =
    let (q , qâ‡“) = dfsá¶œâ‡“ y a (rec y (sub y (here refl)))
        (z , zâ‡“) = dfs-listá¶œâ‡“ l q {x} ac (Î» z Hz â†’ sub z (there Hz))
      in
    z , (map (Î» qke â†’ let (qk , qe) = qke in  -- splitting this directly breaks termination checker for some reason
                   (suc qk)
                 , fun-ext Î» Îº â†’
                     dfs-listáµ (y âˆ· l) a
                       ï¼âŸ¨âŸ©
                     later ((dfs-listáµ l =<<áµ_) â‰ (dfix dfsáµ-body âŠ› next y âŠ› next a))
                       ï¼âŸ¨ ap later (â–¹-ext Î» Î± â†’ ap (dfs-listáµ l =<<áµ_) Î» i â†’ pfix dfsáµ-body i Î± y a) âŸ©
                     Î´áµ (dfs-listáµ l =<<áµ dfsáµ y a)
                       ï¼âŸ¨ ap Î´áµ (happly qe Îº) âŸ©
                     delay-byáµ (suc qk) z
                       âˆ)
             (bindâ‡“ (dfs-listá¶œ l) qâ‡“ zâ‡“))

dfs0á¶œâ‡“ : (x : A) â†’ dfs0á¶œ x â‡“
dfs0á¶œâ‡“ x = dfsá¶œâ‡“ x [] (acy x)

dfsá¶œ-correct : (x : A) â†’ (l : List A)
             â†’ succ-closed l
             â†’ Alláµ– (Î» q â†’ âˆ€ y â†’ (Has y q â†’ Has y l âŠ Star succ y x) Ã— (Has y l âŠ Star succ y x â†’ Has y q)) (dfsá¶œ x l)
dfsá¶œ-correct x l sc Îº = fix {k = Îº} go x l sc
  where
  go-list : â–¹ Îº ((x : A) â†’ (l : List A) â†’ succ-closed l
                  â†’ gAlláµ– Îº (Î» q â†’ (y : A) â†’ (Has y q â†’ Has y l âŠ Star succ y x) Ã— (Has y l âŠ Star succ y x â†’ Has y q)) (dfsáµ x l))
          â†’ (x : A) â†’ (l : List A) â†’ (accu : List A)
          â†’ succ-closed accu
          â†’ (sub : Subset l (sucs x))
          â†’ (âˆ€ z â†’ succ z x â†’ Â¬ Has z l â†’ Has z accu)
          â†’ gAlláµ– Îº (Î» q â†’ (y : A) â†’ (Has y q â†’ Has y accu âŠ Plus succ y x) Ã— (Has y accu âŠ Plus succ y x â†’ Has y q)) (dfs-listáµ l accu)
  go-list ihâ–¹ x []      accu sc sub accm =
    gAll-now Î» y â†’ (inl , [ id
                          , (Î» p â†’ let (s , syc , scx) = Plus-unsnoc p in
                              star-sc accu sc s y (accm s scx Â¬Any-[]) syc)
                          ]áµ¤)
  go-list ihâ–¹ x (w âˆ· l) accu sc sub accm =
    gAll-later Î» Î± â†’
      transport (Î» i â†’ gAlláµ– Îº (Î» q â†’ âˆ€ y â†’ (Has y q â†’ Has y accu âŠ Plus succ y x) Ã— (Has y accu âŠ Plus succ y x â†’ Has y q))
                             (dfs-listáµ l =<<áµ pfix dfsáµ-body (~ i) Î± w accu)) $
      all->>=áµ (ihâ–¹ Î± w accu sc)
        Î» {a} pa â†’
          all-weakenáµ
            (Î» {aâ€²} fgâ€² y â†’
                 (Î» hyaâ€² â†’ [ (Î» hya â†’ [ inl , (Î» yâ†w â†’ inr (Star-â—… yâ†w (sub w (here refl)))) ]áµ¤ (pa y .fst hya)) , inr ]áµ¤ (fgâ€² y .fst hyaâ€²))
               , (Î» h+p â†’ fgâ€² y .snd ([ (Î» hyac â†’ inl (pa y .snd (inl hyac))) , inr ]áµ¤ h+p)))
            (go-list ihâ–¹ x l a
                      (Î» s t hs hts â†’ pa t .snd ([ (Î» hha â†’ inl (sc s t hha hts)) , (Î» str â†’ inr (hts â—… str)) ]áµ¤ (pa s .fst hs)))
                      (Î» z Hz â†’ sub z (there Hz))
                      Î» z Hz Nz â†’ pa z .snd (Dec.rec (Î» e â†’ inr (subst (Star succ z) e Îµ))
                                                     (Î» ne â†’ inl (accm z Hz (Â¬Any-âˆ· (ne âˆ˜ sym) Nz)))
                                                     (z â‰Ÿ w)))

  go : â–¹ Îº ((x : A) â†’ (l : List A) â†’ succ-closed l
            â†’ gAlláµ– Îº (Î» q â†’ (y : A) â†’ (Has y q â†’ Has y l âŠ Star succ y x) Ã— (Has y l âŠ Star succ y x â†’ Has y q)) (dfsáµ x l))
     â†’ (x : A) â†’ (l : List A) â†’ succ-closed l
     â†’ gAlláµ– Îº (Î» q â†’ (y : A) â†’ (Has y q â†’ Has y l âŠ Star succ y x) Ã— (Has y l âŠ Star succ y x â†’ Has y q)) (dfsáµ x l)
  go ihâ–¹ x l sc with has x l | recall (Î» q â†’ has q l) x
  ... | true  | âŸª e âŸ« =
    gAll-now Î» y â†’ (inl , [ id , star-sc l sc x y (true-reflects (has-r x l) (subst âŸ¦_âŸ§áµ‡ (sym e) tt)) ]áµ¤)
  ... | false | _     =
    all-mapáµ
      (Î» fg y â†’ (Î» where
                     (here e) â†’ inr (subst (Star succ y) (sym e) Îµ)
                     (there hy) â†’ [ inl , inr âˆ˜ Plusâ†’Star ]áµ¤ (fg y .fst hy))
                , [ there âˆ˜ fg y .snd âˆ˜ inl , [ there âˆ˜ fg y .snd âˆ˜ inr , (here âˆ˜ sym) ]áµ¤ âˆ˜ Starâ†’Plusï¼ ]áµ¤)
      (go-list ihâ–¹ x (sucs x) l sc
                    (Subset-refl (sucs x)) (Î» z Hz Nz â†’ absurd (Nz Hz)))

dfs0á¶œ-correct : (x : A)
              â†’ Alláµ– (Î» q â†’ âˆ€ y â†’ (Has y q â†’ Star succ y x) Ã— (Star succ y x â†’ Has y q)) (dfs0á¶œ x)
dfs0á¶œ-correct x =
  all-weaken
    (Î» fg y â†’ ([ (Î» h â†’ absurd (Â¬Any-[] h)) , id ]áµ¤ âˆ˜ fg y .fst)
              , (fg y .snd âˆ˜ inr))
    (dfsá¶œ-correct x [] (Î» _ _ h _ â†’ absurd (Â¬Any-[] h)))
